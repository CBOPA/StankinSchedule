apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'

android {
    compileSdkVersion 30
    buildToolsVersion '29.0.2'

    defaultConfig {
        applicationId "com.vereshchagin.nikolay.stankinschedule"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 56
        versionName "1.2.0-beta2"
        vectorDrawables.useSupportLibrary = true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            manifestPlaceholders = [
                    crashlyticsCollectionEnabled: "false"
            ]
        }
        release {
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro'
            manifestPlaceholders = [
                    crashlyticsCollectionEnabled: "true"
            ]
        }
    }

    testOptions {
        unitTests.returnDefaultValues = false
    }

    buildFeatures {
        dataBinding = true
        viewBinding = true
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin library
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'

    // AppCompat
    implementation 'androidx.appcompat:appcompat:1.2.0'

    // Layouts
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.gridlayout:gridlayout:1.0.0'

    // Navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.3.2'
    implementation 'androidx.navigation:navigation-ui-ktx:2.3.2'

    // Preference
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.security:security-crypto:1.1.0-alpha03'

    // Paging and pager
    implementation 'androidx.viewpager2:viewpager2:1.0.0'
    implementation 'androidx.paging:paging-runtime-ktx:3.0.0-alpha11'

    // Work
    def work_version = "2.4.0"
    implementation "androidx.work:work-runtime:$work_version"
    implementation "androidx.work:work-runtime-ktx:$work_version"

    // Material Design
    implementation 'com.google.android.material:material:1.3.0-beta01'

    // Chrome Custom Tabs
    implementation 'androidx.browser:browser:1.3.0'

    // WebView
    implementation 'androidx.webkit:webkit:1.4.0'

    // Google Play Core
    implementation 'com.google.android.play:core-ktx:1.8.1'

    // Firebase
    implementation platform('com.google.firebase:firebase-bom:25.12.0')
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-storage-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'

    // Room DB
    def room_version = "2.3.0-alpha04"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Glide library
    def glide_version = "4.11.0"
    implementation "com.github.bumptech.glide:glide:$glide_version"
    kapt "com.github.bumptech.glide:compiler:$glide_version"

    // Kotlin Components
    def lifecycle_ktx = "2.2.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_ktx"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_ktx"
    implementation "androidx.fragment:fragment-ktx:1.2.5"

    // Network
    implementation 'com.squareup.okhttp3:logging-interceptor:4.8.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.google.code.gson:gson:2.8.6'

    // Other
    implementation 'com.larswerkman:HoloColorPicker:1.5'
    implementation 'com.facebook.shimmer:shimmer:0.5.0'
    implementation 'commons-io:commons-io:2.7'
    implementation 'joda-time:joda-time:2.10.6'


//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.1'
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.json:json:20200518'
    androidTestImplementation 'androidx.test:runner:1.3.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
